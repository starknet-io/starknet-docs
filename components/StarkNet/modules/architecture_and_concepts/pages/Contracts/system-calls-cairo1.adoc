= System Calls

Writing smart contracts requires some operations, such as calling another contract or accessing the contract's storage, that standalone programs do not require. The Starknet contract language supports these operations by using system calls. System calls enable a contract to require services from the Starknet OS. You can use system calls in a function to get information that depends on the broader state of Starknet, which would otherwise be inaccessible, rather than local variables that appear in the function's scope.

[id="get_execution_info"]
== `get_execution_info`

[discrete]
=== Syntax

[source,cairo]
----
extern fn get_execution_info_syscall() -> SyscallResult<Box<starknet::info::ExecutionInfo>> implicits(
    GasBuiltin, System
) nopanic;
----

.Description

Gets information about the original transaction.

In Cairo 1.0, all block/transaction/execution context getters are batched into this single system call.

.Arguments

None.

.Return values

* A link:https://github.com/starkware-libs/cairo/blob/efbf69d4e93a60faa6e1363fd0152b8fcedbb00a/corelib/src/starknet/info.cairo#L8[struct] containing the execution info.

.Common library

link:https://github.com/starkware-libs/cairo/blob/cca08c898f0eb3e58797674f20994df0ba641983/corelib/src/starknet/syscalls.cairo#L35[`syscalls.cairo`^]

[id="call_contract"]
== `call_contract`

.Description

Calls a given contract. This system call expects the address of the called contract, a selector for a function within that contract, and call arguments.

[discrete]
==== Syntax

[source,cairo,subs="+quotes,+macros"]
----
extern fn call_contract_syscall(
    address: ContractAddress, entry_point_selector: felt252, calldata: Span<felt252>
) -> SyscallResult<Span<felt252>> implicits(GasBuiltin, System) nopanic;
----

.Arguments

[horizontal,labelwidth=35]
`_address_`:: The address of the contract you want to call.
`_entry_point_selector_`:: A selector for a function within that contract.
`_calldata_`:: The calldata array.

.Return values

* The call response, of type `SyscallResult<Span<felt252>>`.


.Common library

link:https://github.com/starkware-libs/cairo/blob/cca08c898f0eb3e58797674f20994df0ba641983/corelib/src/starknet/syscalls.cairo#L10[`syscalls.cairo`^]

[NOTE]
====
This is considered a lower-level syntax for calling contracts.

If the interface of the called contract is available, then you can use a more straightforward syntax.
====

[id="deploy"]
== `deploy`

[discrete]
===== Syntax

[source,cairo,subs="+quotes,+macros"]
----
extern fn deploy_syscall(
    class_hash: ClassHash,
    contract_address_salt: felt252,
    calldata: Span<felt252>,
    deploy_from_zero: bool,
) -> SyscallResult<(ContractAddress, Span::<felt252>)> implicits(GasBuiltin, System) nopanic;
----

.Description

Deploys a new instance of a previously declared class.

.Arguments

[horizontal,labelwidth=35]
`_class_hash_`:: The class hash of the contract to be deployed.
`_contract_address_salt_`:: The salt, an arbitrary value provided by the sender, used in the computation of the xref:Contracts/contract-address.adoc[contract's address].
`_calldata_`:: The constructor's calldata. An array of felts.
`_deploy_from_zero_`:: A flag used for the contract address computation. If not set, the caller address will be used as the new contract's deployer address, otherwise 0 is used.

.Return values

* A tuple wrapped with `SyscallResult` where:
** The first element is the address of the deployed contract, of type `ContractAddress`.
** The second element is the response array from the contract's constructor, of type `Span::<felt252>`.

.Common library

link:https://github.com/starkware-libs/cairo/blob/main/corelib/src/starknet/syscalls.cairo#L20[`syscalls.cairo`^]


[id="emit_event"]
== `emit_event`

.Syntax
{empty}ㅤㅤ
[source,cairo,subs="+quotes,+macros"]
----
extern fn emit_event_syscall(
    keys: Span<felt252>, data: Span<felt252>
) -> SyscallResult<()> implicits(GasBuiltin, System) nopanic;
----

.Description

Emits an event with a given set of keys and data.

For more information, and for a higher-level syntax for emitting events, see xref:Events/starknet-events.adoc[Starknet events].

.Arguments

[horizontal,labelwidth=35]
`_keys_`:: The event's keys. These are analogous to Ethereum's event topics, you can use the link:https://github.com/starkware-libs/starknet-specs/blob/c270b8170684bb09741672a7a4ae5003670c3f43/api/starknet_api_openrpc.json#L569RPC[starknet_getEvents] method to filter by these keys.
`_data_`:: The event's data.

.Return values

None.

.Common library

link:https://github.com/starkware-libs/cairo/blob/cca08c898f0eb3e58797674f20994df0ba641983/corelib/src/starknet/syscalls.cairo#L30[`syscalls.cairo`^]

.Example

The following example emits an event with two keys, the strings `status` and `deposit` and three data elements: `1`, `2`, and `3`.

[source,cairo]
----
let keys = ArrayTrait::new();
keys.append('key');
keys.append('deposit');
let values = ArrayTrait::new();
values.append(1);
values.append(2);
values.append(3);
emit_event_syscall(keys, values).unwrap_syscall();
----

[id="library_call"]
== `library_call`

.Syntax
{empty}ㅤㅤ
[source,cairo,subs="+quotes,+macros"]
----
extern fn library_call_syscall(
    class_hash: ClassHash, function_selector: felt252, calldata: Span<felt252>
) -> SyscallResult<Span<felt252>> implicits(GasBuiltin, System) nopanic;
----

.Description

Calls the requested function in any previously declared class. The class is only used for its logic.

This system call replaces the known delegate call functionality from Ethereum, with the important difference that there is only one contract involved.

.Arguments

[horizontal,labelwidth=35]
`_class_hash_`:: The hash of the class you want to use.
`_function_selector_`:: A selector for a function within that class.
`_calldata_`:: The calldata.

.Return values

* The call response, of type `SyscallResult<Span<felt252>>`.

.Common library

link:https://github.com/starkware-libs/cairo/blob/cca08c898f0eb3e58797674f20994df0ba641983/corelib/src/starknet/syscalls.cairo#L43[`syscalls.cairo`^]

[id="send_message_to_L1"]
== `send_message_to_L1`

.Syntax
{empty}ㅤㅤ
[source,cairo,subs="+quotes,+macros"]
----
extern fn send_message_to_l1_syscall(
    to_address: felt252, payload: Span<felt252>
) -> SyscallResult<()> implicits(GasBuiltin, System) nopanic;
----

.Description

Sends a message to L1.

This system call includes the message parameters as part of the proof's output and exposes these parameters to the Starknet Core contract on L1 once the state update, including the transaction, is received.

For more information, see Starknet's xref:L1-L2_Communication/messaging-mechanism.adoc[messaging mechanism].

.Arguments

[horizontal,labelwidth=35]
`_to_address_`:: The recipient's L1 address.
`_payload_`:: The array containing the message payload

.Return values

None.

.Common library

link:https://github.com/starkware-libs/cairo/blob/cca08c898f0eb3e58797674f20994df0ba641983/corelib/src/starknet/syscalls.cairo#L51[`syscalls.cairo`^]

.Example

The following example sends a message whose content is `(1,2)` to the L1 contract whose address is `3423542542364363`.

[source,cairo,subs="+quotes,+macros"]
----
let payload = ArrayTrait::new();
payload.append(1);
payload.append(2);
send_message_to_l1_syscall(payload).unwrap_syscall();
----

[id="replace_class"]
== `replace_class`

.Syntax
{empty}ㅤㅤ
[source,cairo,subs="+quotes,+macros"]
----
extern fn replace_class_syscall(
    class_hash: ClassHash
) -> SyscallResult<()> implicits(GasBuiltin, System) nopanic;
----

.Description
Once `replace_class` is called, the class of the calling contract (i.e. the contract whose address is returned by `get_contract_address` at the time the syscall is called) will be replaced
by the class whose hash is given by the class_hash argument.

[NOTE]
====
After calling `replace_class`, the code currently executing from the old class will finish running.


The new class will be used from the next transaction onwards or if the contract is called via
the `call_contract` syscall in the same transaction (after the replacement).
====

.Arguments

[horizontal,labelwidth=35]
`_class_hash_`:: The hash of the class you want to use as a replacement.

.Return values

None.

.Common library
link:https://github.com/starkware-libs/cairo/blob/cca08c898f0eb3e58797674f20994df0ba641983/corelib/src/starknet/syscalls.cairo#L77[`syscalls.cairo`^]

[id="storage_read"]
== `storage_read`

.Syntax
{empty}ㅤㅤ
[source,cairo,subs="+quotes,+macros"]
----
extern fn storage_read_syscall(
    address_domain: u32, address: StorageAddress,
) -> SyscallResult<felt252> implicits(GasBuiltin, System) nopanic;
----

.Description

Gets the value of a key in the storage of the calling contract.

This system call provides direct access to any possible key in storage, in contrast with `var.read()`, which enables you to read storage variables that are defined explicitly in the contract.

For information on accessing storage by using the storage variables, see xref:./contract-storage.adoc#storage_variables[storage variables].

.Arguments

[horizontal,labelwidth=35]
`_address_domain_`:: The domain of the key, used to separate between different data availability modes. This separation is used in Starknet to offer different data availability modes. Currently, only the on-chain mode (where all updates go to L1), indicated by domain `0`, is supported. Other address domains which will be introduced in the future will behave differently in terms of publication (in particular, they will not be posted on L1, creating a tradeoff between cost and security).
`_address_`:: The requested storage address.

.Return values

* The value of the key, of type `SyscallResult<felt252>`.

.Common library

link:https://github.com/starkware-libs/cairo/blob/cca08c898f0eb3e58797674f20994df0ba641983/corelib/src/starknet/syscalls.cairo#L60[`syscalls.cairo`^]

.Example
{empty}
[source,cairo,subs="+quotes,+macros"]
----
use starknet::storage_access::storage_base_address_from_felt252;

...

let storage_address = storage_base_address_from_felt252(3534535754756246375475423547453)
storage_read_syscall(0, storage_address).unwrap_syscall()
----

[id="storage_write"]
== `storage_write`

.Syntax
{empty}
[source,cairo,subs="+quotes,+macros"]
----
extern fn storage_write_syscall(
    address_domain: u32, address: StorageAddress, value: felt252
) -> SyscallResult<()> implicits(GasBuiltin, System) nopanic;
----

.Description

Sets the value of a key in the storage of the calling contract.

This system call provides direct access to any possible key in storage, in contrast with `var.write()`, which enables you to write to storage variables that are defined explicitly in the contract.

For information on accessing storage by using the storage variables, see xref:./contract-storage.adoc#storage_variables[storage variables].

.Arguments

[horizontal,labelwidth=35]
`_address_domain_`:: The domain of the key, used to separate between different data availability modes. This separation is used in Starknet to offer different data availability modes. Currently, only the on-chain mode (where all updates go to L1), indicated by domain `0`, is supported. Other address domains which will be introduced in the future will behave differently in terms of publication (in particular, they will not be posted on L1, creating a tradeoff between cost and security).
`_address_`:: The requested storage address.
`_value_`:: The value to write to the key.

.Return values

None.

.Common library

link:https://github.com/starkware-libs/cairo/blob/cca08c898f0eb3e58797674f20994df0ba641983/corelib/src/starknet/syscalls.cairo#L70[`syscalls.cairo`^]
